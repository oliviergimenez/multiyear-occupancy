
model
{
  
  # State model priors
  b[1] ~ dnorm(mu.b, 1)           # random walk prior on year effect
  for(j in 2:nyears){
    b[j] ~ dnorm(b[j-1], tau.b)
  }
  tau.b <- 1 / (sd.b * sd.b)
  mu.b ~ dnorm(0, 1)
  sd.b ~ dunif(0, 5)		# half-uniform hyperpriors
  
  for (i in 1:nsites){
    u[i] ~ dnorm(0, tau.u) 	# random site effect      
  } 
  tau.u <- 1 / (sd.u * sd.u)
  sd.u ~ dunif(0, 5)		# half-uniform hyperpriors
  
  ## priors for alpha and beta coeffs
  for(i in 1:2) {
    beta[i]  ~ dnorm(0, 1)
  }
  for(i in 1:7) {
    alpha[i] ~ dnorm(0, 1)
  }
  for(i in 1:nsites) {
    for(j in 1:nyears) {
      logit(phi[i,j]) <- alpha[1] * forest[i] + alpha[2] * shrubst[i] + alpha[3] * agri21[i] + 
      alpha[4] * agri22[i] + alpha[5] * dtohighway[i] + alpha[6] * humdens[i] + 
      alpha[7] * lengthroad[i] + b[j] + u[i]
      z[i,j] ~ dbern(phi[i,j])  ## true pres/absences states
      lp[i,j] <- beta[1] + beta[2] * effort[i,j]
      p[i,j] <- (1 / (1 + exp(-lp[i,j]))) * (1 - step(-binary.effort[i,j]))
      y[i,j] ~ dbin(p[i,j] * z[i, j], nvisits)  ## likelihood
    }
  }
  
  # Finite sample occupancy - proportion of occupied sites
#  for (j in 1:nyears) {
#    psi.fs[j] <- sum(z[1:nsites,j]) / nsites.year[j]
#  }

#    # Also save recent maps
#    for(i in 1:nsites) {
#      map1[i] <- z[i, nyears - 2]
#      map2[i] <- z[i, nyears - 1]
#      map3[i] <- z[i, nyears]
#    }
}

